#include "RPG.dh"

#include "Arena.dh"
#include "Augs.dh"
// #include "GUI.dh"
// #include "GUIMenu.dh"
#include "HealthBars.dh"
#include "HUD.dh"
#include "ItemData.dh"
#include "Menu.dh"
#include "Mission.dh"
#include "Monsters.dh"
#include "Outpost.dh"
#include "Popoffs.dh"
#include "Shield.dh"
#include "Shop.dh"
#include "Skills.dh"
#include "Stats.dh"
#include "Stims.dh"
#include "Utils.dh"

// Version
str Version = "v0.9.11 Beta - Gotta Go Fast Edition";

// Flags
static bool InBase;
static bool Transported;
static bool GlobalsInitialized;
    
// Arrays
static str[MAX_PLAYERS] PlayerWeapon;

// Structs
static PlayerData[MAX_PLAYERS] _PlayerData;
// static MinigameData[MAX_PLAYERS] _MinigameData;

// --------------------------------------------------
// Multiplayer HUD
// 

// HUD Combo
GlobalArray(20) int[MAX_PLAYERS] Combo @ 0;
GlobalArray(22) int[MAX_PLAYERS] BonusGained @ 0;
GlobalArray(23) int[MAX_PLAYERS] XPGained @ 0;
GlobalArray(24) int[MAX_PLAYERS] RankGained @ 0;

// HUD Timers
GlobalArray(25) int[MAX_PLAYERS] AuraTimerHUD @ 0;
GlobalArray(26) int[MAX_PLAYERS] RegenBoostTimerHUD @ 0;
GlobalArray(42) int[MAX_PLAYERS] StimTimerHUD @ 0;

// HUD Item Counts
GlobalArray(18) int[MAX_PLAYERS] Credits @ 0;
GlobalArray(19) int[MAX_PLAYERS] Continues @ 0;

// Max Health and Armor
GlobalArray(39) int[MAX_PLAYERS] HealthMax @ 0;
GlobalArray(40) int[MAX_PLAYERS] ArmorMax @ 0;

// Energy Points (EP)
GlobalArray(2)  int[MAX_PLAYERS] EP @ 0;
GlobalArray(3)  int[MAX_PLAYERS] EPMax @ 0;

// Shield
GlobalArray(50) int[MAX_PLAYERS] Shield @ 0;
GlobalArray(51) int[MAX_PLAYERS] ShieldCapacity @ 0;
GlobalArray(52) int[MAX_PLAYERS] ShieldHealth @ 0;

script void GlobalInit() open
{
    if (!GlobalsInitialized)
    {
        // Version Info
        Log("\cnDoom RPG %s (Compiled on %s at %s) loaded!\n", Version, __DATE__, __TIME__);
        
        // Compatibility checking
        CheckCompatibility();
        
        // Get the XP Curve from the CVAR
        XPCurve = GetCVar("drpg_xp_curve");
        
        // Initialize XP & Rank Tables
        InitTables();
        
        GlobalsInitialized = true;
        
        Delay (1); // Allow CVARs to update
    };
    
    // Create Translations
    CreateTranslations();
    
    // Initial build of ItemData
    BuildItemData();
};

// Init Script
script void Init() enter
{
    // Wait until globals are initialized
    while (!GlobalsInitialized) Delay(1);
    
    // [KS] This needs to be done on its' own, so death exits don't rob you of your TID and break EVERYTHING EVER FFS
    if (ActivatorTID() == 0)
    {
        AssignTIDs();
        DefaultLoadout();
    };
    
    if (!Player.FirstRun)
    {
        // Setup Stats and Special Starts
        if (GetCVar("drpg_special_start") == 0) // None
        {
            Player.Vitality = 10;
            Player.Energy = 10;
            Player.Capacity = 10;
            
            Player.EP = Player.Energy * 10;
        };
        if (GetCVar("drpg_special_start") == 1) // Luck Based
        {
            Player.Defense = -100;
            Player.Vitality = 1;
            Player.Regeneration = -100;
            Player.Capacity = 5;
            Player.Luck = 100;
            
            SetActorProperty(0, APROP_Health, Player.Vitality * 10);
            Player.EP = Player.Energy * 10;
        };
        if (GetCVar("drpg_special_start") == 2) // Speed Demon
        {
            Player.Defense = -200;
            Player.Vitality = 10;
            Player.Energy = 0;
            Player.Agility = 100;
            Player.Capacity = 10;
            
            SetActorProperty(0, APROP_Health, Player.Vitality * 10);
            Player.EP = Player.Energy * 10;
        };
        if (GetCVar("drpg_special_start") == 3) // The Magic Man
        {
            Player.Defense = -100;
            Player.Vitality = 5;
            Player.Energy = 100;
            Player.Capacity = 5;
            
            SetActorProperty(0, APROP_Health, Player.Vitality * 10);
            Player.EP = Player.Energy * 10;
        };
        
        // Set the default skill indices
        for (int i = 0; i < MAX_SKILLKEYS; i++)
        {
            Player.SkillCategory[i] = -1;
            Player.SkillIndex[i] = -1;
        };
        
        // Set default selected skill to nothing
        Player.SkillSelected = -1;
        
        // Fill Augmentation Battery
        Player.Augs.Battery = 100;
        
        // Done first run
        Player.FirstRun = true;
    };
    
    // Apply camera textures
    SetCameraToTexture(Player.TID, StrParam("P%iVIEW\n", PlayerNumber() + 1), 110);
    
    // Remove Aura if the keep Aura CVAR is off
    if (!GetCVar("drpg_skill_keepauras"))
        RemoveAura();
    
    // Transport from/to Outpost Screen Fading
    if (Transported)
    {
        for (int i = 0; i < MAX_PLAYERS; i++)
            SetActorProperty(Players(i).TID, APROP_Invulnerable, false);
        SetPlayerProperty (0, 0, PROP_TOTALLYFROZEN); // In case we were in the menu
        FadeRange(255, 255, 255, 1.0, 255, 255, 255, 0.0, 1.0);
        Transported = false;
    };
    
    // Clear the Player's summons
    for (int i = 0; i < MAX_SUMMONS; i++)
        Player.SummonTID[i] = 0;
    Player.Summons = 0;
    
    // Clear the DropTID array
    for (int i = 0; i < MAX_DROPS; i++)
        Player.DropTID[i] = -1;
    
    // Initialize skill array
    InitSkills();
    
    // Initial build of SkillData
    BuildSkillData();
    
    // Mission Initialization
    InitMission();

    // Small delay before initializing the scripts for mod compatibility
    Delay(4);
    
    // Some mods will require TID reassignment
    if (GetCVar("drpg_ext_brutal"))
        AssignTIDs();
    
    // Execute Game Loops
    Loop();
    ShieldScript();
    WeaponSpeed();
    StatRandomizer();
    AutosaveHandler();
    DamageNumbers();
    InfoPopoffs();
    HealthBars();
    AutoSellHandler();
    
    // GUI
	// CheckCursor();
	// CheckGUI();
	// MenuUpdate();
};

// Loop Script
script void Loop()
{
    int OldDamage;
    
    Start:
    
    // If the player's dead, terminate
    if (GetActorProperty(0, APROP_Health) <= 0)
    {
        // Show the killing blow
        CheckDamageHUD();
        return;
    };
    
    // Update Functions
    CheckCombo();
    CheckStats();
    CheckStatBonus();
    CheckHardStatCaps();
    CheckRegen();
    CheckLuck();
    CheckHealth();
    CheckArmorMax();
    CheckAugSlots();
    CheckAugs();
    CheckLevel();
    CheckRank();
    CheckSkills();
    CheckBurnout();
    CheckShields();
    CheckShieldAccessory();
    CheckStim();
    CheckPerks();
    CheckStatBounds();
    CheckToxicity();
    CheckStatusEffects();
    CheckStatusDamage();
    CheckCapacity();
    CheckShopCard();
    CheckMission();
    CheckHUD();
    CheckDamageHUD();
    
    // Handle the menu cursor color
    MenuCursorColor = CursorColors[(Timer() / 3) % 6];
    
    // Calculate Shop Discount
    Player.ShopDiscount = (int)((Player.RankLevel * 2.1) + (InBase ? (Player.ShopCard * 5) : 0));
    
    // Auto-Sell Timer Handling
    if (Player.AutoSellTimer > 0)
        Player.AutoSellTimer--;
    
    // Main Menu
    if (Player.InMenu)
        MenuLoop();
    
    // Shop Menu
    if (Player.InShop)
        ShopLoop();
    
    // Menu freezing/dimming
    if (Player.InMenu || Player.InShop || Player.OutpostMenu > 0)
    {
        if (GetCVar("drpg_menufreeze") && !InMultiplayer)
            GiveInventory("DRPGMenuFreezer", 1);
        if (GetCVar("drpg_menudim") && !Player.StatusType[SE_BLIND])
            FadeRange(0, 0, 0, 0.5, 0, 0, 0, 0.0, 0.25);
    };
    
    // Menu-specific Help
    if (Player.InMenu || Player.InShop || Player.OutpostMenu > 0)
        MenuHelp();
    
    // Menu icon in multiplayer
    if (InMultiplayer && (Player.InMenu || Player.InShop || Player.OutpostMenu > 0))
    {
        fixed X = GetActorX(Player.TID);
        fixed Y = GetActorY(Player.TID);
        fixed Z = GetActorZ(Player.TID);
        fixed Height = GetActorPropertyFixed(Player.TID, APROP_Height);
        
        SpawnForced("DRPGMenuIcon", X, Y, Z + Height + 8.0, 0, 0);
    };
    
    // Shop Special Handling
    if (Arbitrator) CheckShopSpecial(false);
    
    // Always Quick Heal if CVAR is set
    if (GetCVar("drpg_auto_heal"))
        QuickHeal(false);
    
    // Check Health/Shield for this tic
    Player.BeforeHealth = GetActorProperty(Player.TID, APROP_Health);
    Player.BeforeShield = Player.Shield.Charge;
    
    // Quick temporary little shield hack to prevent you from exploding if you still have charge left in it
    if (Player.Shield.Active && Player.Shield.Charge > 0)
        SetPlayerProperty(0, 1, PROP_BUDDHA)
    else
        SetPlayerProperty(0, 0, PROP_BUDDHA);
    
    // Regeneration
    if (ClassifyActor(Player.TID) & ACTOR_ALIVE)
        DoRegen();
    
    // Give the Strength boosting item
    if (Player.TotalDamage != OldDamage)
    {
        if (OldDamage)
            TakeInventory(StrParam("DRPGStrength%d\n", OldDamage), 1);
        
        if (Player.TotalDamage >= 32000)
            GiveInventory("DRPGStrength32000", 1)
        else if (Player.TotalDamage > 0)
            GiveInventory(StrParam("DRPGStrength%d\n", Player.TotalDamage), 1);
        
        OldDamage = Player.TotalDamage;
    };
    
    // Set mass stupid high when Invulnerable to prevent knockback
    if (CheckInventory("PowerInvulnerable"))
        Player.Mass *= 128;
    
    // Survival Bonus
    if (RandomFixed(0.0, 100.0) <= Player.SurvivalBonus)
    {
        SetPlayerProperty(0, 1, PROP_BUDDHA);
        
        if (GetActorProperty(Player.TID, APROP_Health) == 1)
        {
            if (Player.Shield.Accessory == SHIELD_ACC_UW0T && !Player.Shield.Active)
            {
                Player.Shield.Charge = Player.Shield.Capacity;
                ToggleShield();
            };
            
            HealThing(1);
            ActivatorSound("health/survive", 127);
            SetHudSize(100, 100, false);
            SetFont("PINSA0");
            HudMessage("A\n", HUDMSG_FADEOUT | HUDMSG_ADDBLEND, 0, CR_UNTRANSLATED, 61.4, 89.4, 0.5, 0.5);
        };
    }
    else
        SetPlayerProperty(0, 0, PROP_BUDDHA);
    
    // Continue check
    if (CheckInventory("DRPGContinue") > 0)
    {
        SetPlayerProperty(0, 1, PROP_BUDDHA);
        
        if (GetActorProperty(Player.TID, APROP_Health) <= 1)
        {
            HealThing(1000000);
            ActivatorSound("health/resurrect", 127);
            TakeInventory("DRPGContinue", 1);
        };
    };
    
    // Apply Stats
    SetActorProperty(Player.TID, APROP_SpawnHealth, Player.HealthMax);
    SetActorPropertyFixed(Player.TID, APROP_DamageFactor, Player.DamageFactor);
    SetActorProperty(Player.TID, APROP_Mass, Player.Mass);
    SetActorPropertyFixed(Player.TID, APROP_Speed, Player.Speed);
    SetActorPropertyFixed(Player.TID, APROP_JumpZ, Player.JumpHeight);
    
    // Make you invincible while in the Menus and if menu freezing is enabled
    if ((Player.InMenu || Player.InShop) && GetCVar("drpg_menufreeze"))
        SetActorProperty(Player.TID, APROP_DamageFactor, 0);
    
    // Reset Damage Type
    Player.DamageType = DT_NONE;
    
    // Store your current weapon in the Player Weapon array
    PlayerWeapon[PlayerNumber()] = GetWeapon();
    
    // Loop
    Delay(1);
    
    // this needs to be done in a separate tic
    Player.AfterHealth = GetActorProperty(Player.TID, APROP_Health);
    Player.HealthLoss = Player.BeforeHealth - Player.AfterHealth;
    Player.AfterShield = Player.Shield.Charge;
    Player.ShieldLoss = Player.BeforeShield - Player.AfterShield;
    
    // Cap health at 200% of your max based on Vitality
    if (GetActorProperty(0, APROP_Health) > Player.Vitality * 10 * 2 && !Player.Shield.Active)
        SetActorProperty(0, APROP_Health, Player.Vitality * 10 * 2);
    
    goto Start;
};

// Handles Weapon Firing Speed
script void WeaponSpeed()
{
    Start:
    int Time;
    
    if (Player.Agility <= 100)
        Time = Abs(Round(Player.WeaponSpeed * 0.35 - 35.0))
    else
        Time = 0;
    
    if (GetCVar("drpg_stat_weaponspeed"))
        GiveInventory("DRPGSpeed", 1);
    
    Delay(Time + 1);
    goto Start;
};

// Stat Randomizer Script
script void StatRandomizer()
{
    Start:
    
    if (GetCVar("drpg_auto_spend") && !Player.Stim.Active)
        while (CheckInventory("DRPGStatToken") > 0)
        {
            if (Random(1, 2) == 1 && GetCVar("drpg_auto_spend_pref") > 0) // Select Preferred Stat
                IncreaseStat(GetCVar("drpg_auto_spend_pref"))
            else
                IncreaseStat(Random(1, 8));
            
            // Should prevent runaway issues if you have a ton of Stat Tokens
            Delay(1);
        };
    
    Delay(1);
    goto Start;
};

// Handles autosaving
script void AutosaveHandler()
{
    // Terminate if the autosave CVar is disabled
    if (GetCVar("drpg_autosave") == 0 || (PlayerCount() > 1 && !Arbitrator)) return;
    
    int SaveTimer = GetCVar("drpg_autosave") * (35 * 60);
    bool Safe;
    bool DamageTimer;
    
    while (true)
    {
        if (Player.HealthLoss)
            DamageTimer = 0
        else
            ++DamageTimer;
        
        if ((Player.InMenu || Player.InShop) && !GetCVar("drpg_menufreeze"))
            DamageTimer = ASAVE_SAFETIME;
        
        if (!SaveTimer)
        {
            Safe = true;
            
            if (DamageTimer < ASAVE_SAFETIME ||
                GetActorProperty(Player.TID, APROP_Health) <= GetActorProperty(Player.TID, APROP_SpawnHealth) / 10)
                Safe = false;
            
            if (Safe)
            {
                Autosave();
                SaveTimer = GetCVar("drpg_autosave") * (35 * 60);
            }
            else
                SaveTimer = ASAVE_RETRYTIME;
        }
        else
            --SaveTimer;
        
        Delay(1);
    };
};

// Quick Heal
acscript void QuickHeal(int Quick) net
{
    // If you're dead, return
    if (ClassifyActor(Player.TID) & ACTOR_DEAD) return;
    
    int Health = GetActorProperty(Player.TID, APROP_Health);
    int Percent = GetCVar("drpg_auto_heal_percent");
    
    if ((Health < Player.HealthMax / Percent) || Quick)
    {
        if (GetCVar("drpg_auto_heal_order") == 1) // Smallest to Largest
        {
                 if (CheckInventory("DRPGStimPack"))            UseInventory("DRPGStimPack")
            else if (CheckInventory("DRPGMedikit"))             UseInventory("DRPGMedikit")
            else if (CheckInventory("DRPGLargeMedikit"))        UseInventory("DRPGLargeMedikit")
            else if (CheckInventory("DRPGXLMedikit"))           UseInventory("DRPGXLMedikit")
            else if (CheckInventory("DRPGMedPack"))             UseInventory("DRPGMedPack")
            else if (CheckInventory("DRPGSurgeryKit"))          UseInventory("DRPGSurgeryKit");
        }
        else if (GetCVar("drpg_auto_heal_order") == 2) // Largest to Smallest
        {
                 if (CheckInventory("DRPGSurgeryKit"))          UseInventory("DRPGSurgeryKit")
            else if (CheckInventory("DRPGMedPack"))             UseInventory("DRPGMedPack")
            else if (CheckInventory("DRPGXLMedikit"))           UseInventory("DRPGXLMedikit")
            else if (CheckInventory("DRPGLargeMedikit"))        UseInventory("DRPGLargeMedikit")
            else if (CheckInventory("DRPGMedikit"))             UseInventory("DRPGMedikit")
            else if (CheckInventory("DRPGStimPack"))            UseInventory("DRPGStimPack");
        };
    };
};

// Quickly buy EP
acscript void QuickEP() net
{
    /* Disable this for now to try and make everyone happy
    if (!GetCVar("drpg_shoptype") && !InBase)
    {
        ActivatorSound("menu/error", 127);
        return;
    }; */
    
    int Amount = Player.EPMax - Player.EP;
    
    if (CheckInventory("DRPGCredits") > 0 && Player.EP < Player.EPMax)
    {
        ActivatorSound("health/epcapsule", 127);
        
        if (CheckInventory("DRPGCredits") < Amount)
            Amount = CheckInventory("DRPGCredits");
        
        TakeInventory("DRPGCredits", Amount);
        AddEP(Amount);
    }
    else
    {
        ActivatorSound("menu/error", 127);
        return;
    };
};

// Death Script
script void Dead() death
{
    // Taunt the Player
    ActivatorSound("Taunt", 127);
    
    // Reset menu vars
    Player.InMenu = false;
    Player.InShop = false;
    Player.OutpostMenu = 0;
    
    // Remove Aura
    RemoveAura();
    
    // Remove Shield
    SetInventory("DRPGShield", 0);
    
    // Remove Status Effect
    for (int i = 0; i < SE_MAX; i++)
        Player.StatusTimer[i] = 0;
    
    // Drop Credits
    if (GetCVar("drpg_multi_dropcredits") && CheckInventory("DRPGCredits") > 0)
    {
        int DropAmount = CheckInventory("DRPGCredits") / 100 * GetCVar("drpg_multi_dropcredits_percent");
        
        // Cap out at a million so if you have stupid amounts of Credits you don't freeze/nuke the game
        if (DropAmount > 1000000) DropAmount = 1000000;
        
        TakeInventory("DRPGCredits", DropAmount);
        DropMoney(PlayerNumber(), 0, DropAmount);
    };
    
    // Drop Inventory
    if (GetCVar("drpg_multi_dropinv"))
        DropInventory();
    
    // Remove TID
    Thing_ChangeTID(Player.TID, 0);
};

// Respawn
script void Respawn() respawn
{
    // Reassign TID
    Player.TID = PLAYER_TID + PlayerNumber();
    Thing_ChangeTID(0, Player.TID);
    
    if (GetCVar("drpg_debug"))
        Log("\cdDEBUG: Player TID: %d\n", Player.TID);
    
    // Heal to max health
    SetActorProperty(0, APROP_Health, Player.HealthMax);
    
    // XP/Rank Penalty
    if (GetCVar("drpg_multi_takexp"))
    {
        int XPPenalty = Abs(Round((fixed)XPTable[Player.Level] / 100.0 * GetCVarFixed("drpg_multi_takexp_percent")));
        int RankPenalty = Abs(Round((fixed)RankTable[Player.RankLevel] / 100.0 * GetCVarFixed("drpg_multi_takexp_percent")));
        
        if (XPPenalty > 0 && RankPenalty > 0)
        {
            Player.XP -= XPPenalty;
            Player.Rank -= RankPenalty;
            SetFont("BIGFONT");
            HudMessage("\cjXP -%d\n\ckRank -%d\n", XPPenalty, RankPenalty, HUDMSG_FADEOUT | HUDMSG_LOG, 0, CR_WHITE, 1.5, 0.75, 2.0, 2.0);
        };
    };
    
    // Restore EP if CVAR is set
    if (GetCVar("drpg_multi_restoreep"))
        Player.EP = Player.EPMax;
    
    // Give a box of ammo if a specific ammo type is empty if the CVAR is set
    if (GetCVar("drpg_multi_restoreammo"))
    {
        if (CheckInventory("Clip") < GetAmmoAmount("Clip") * (Player.Capacity / 10))
            SetInventory("Clip", GetAmmoAmount("Clip") * (Player.Capacity / 10));
        if (CheckInventory("Shell") < GetAmmoAmount("Shell") * (Player.Capacity / 10))
            SetInventory("Shell", GetAmmoAmount("Shell") * (Player.Capacity / 10));
        if (CheckInventory("RocketAmmo") < GetAmmoAmount("RocketAmmo") * (Player.Capacity / 10))
            SetInventory("RocketAmmo", GetAmmoAmount("RocketAmmo") * (Player.Capacity / 10));
        if (CheckInventory("Cell") < GetAmmoAmount("Cell") * (Player.Capacity / 10))
            SetInventory("Cell", GetAmmoAmount("Cell") * (Player.Capacity / 10));
    };
    
    // Run Scripts
    Loop();
    DamageNumbers();
    InfoPopoffs();
    HealthBars();
};

// Apply values to global vars visible to the HUD
function void CheckHUD()
{
    // New Multiplayer HUD Checking
    int PlayerNum = PlayerNumber();
    
    // Max Health/Armor
    HealthMax[PlayerNum] = Player.HealthMax;
    ArmorMax[PlayerNum] = Player.ArmorMax;
    
    // EP
    EP[PlayerNum] = Player.EP;
    EPMax[PlayerNum] = Player.EPMax;
    
    // EP Bar on HUD
    if (Player.EPMax == 0)
        SetInventory("DRPGEP", 0)
    else
        SetInventory("DRPGEP", Player.EP * 100 / Player.EPMax);
    
    // Combo System
    Combo[PlayerNum] = Player.Combo;
    BonusGained[PlayerNum] = Player.BonusGained;
    XPGained[PlayerNum] = Player.XPGained;
    RankGained[PlayerNum] = Player.RankGained;
    
    // Regen Boost
    SetInventory("DRPGRegenBoost", (Player.RegenBoostTimer > 0));
    RegenBoostTimerHUD[PlayerNum] = Player.RegenBoostTimer / 35 + 1;
    
    // For Combo Bar on HUD
    if (Player.ComboTimer < 35 * 4 && Player.ComboTimer > 35 * 2)
    {
        SetInventory("DRPGCombo", (int)((fixed)Player.ComboTimer / (((fixed)COMBO_MAX) / 100.0)));
        SetInventory("DRPGCombo2", 0);
    }
    else if (Player.ComboTimer <= 35 * 2)
    {
        SetInventory("DRPGCombo", 0);
        SetInventory("DRPGCombo2", (int)((fixed)Player.ComboTimer / (((fixed)COMBO_MAX) / 100.0)));
    };
    
    // Stim Timer
    SetInventory("DRPGStimActive", Player.Stim.Active);
    if (Player.Stim.Timer > 0)
        StimTimerHUD[PlayerNum] = Player.Stim.Timer / 35 + 1
    else
        StimTimerHUD[PlayerNum] = 0;
    
    // Shield
    if (Player.Shield.Capacity > 0)
    {
        Shield[PlayerNum] = Player.Shield.Charge;
        ShieldCapacity[PlayerNum] = Player.Shield.Capacity;
        ShieldHealth[PlayerNum] = Player.Shield.Health;
        SetInventory("DRPGShieldCapacity", (int)(((fixed)Player.Shield.Charge / (fixed)Player.Shield.Capacity) * 100.0));
        SetInventory("DRPGShield", Player.Shield.Active);
    };
    
    // Items
    Credits[PlayerNum] = CheckInventory("DRPGCredits");
    Continues[PlayerNum] = CheckInventory("DRPGContinue");
};

function void DefaultLoadout()
{
    // Extras Compatibility
    if(GetCVar("drpg_ext_extras"))
        SetWeapon("Pistol1");
    
    // DoomRL Compatibility
    if (GetCVar("drpg_ext_doomrl"))
        SetWeapon("RLPistol");
    
    // Brutal Doom Compatibility
    if (GetCVar("drpg_ext_brutal"))
    {
        GiveInventory("BrutalPistol", 1);
        GiveInventory("BDPistolAmmo", 15);
        TakeInventory("NewClip", 25);
    };
};

function void CheckCompatibility()
{
    bool Success = false;
    int TID = UniqueTID();
    
    // Reset CVARs
    SetCVar("drpg_ext_extras", 0);
    SetCVar("drpg_ext_doomrl", 0);
    SetCVar("drpg_ext_brutal", 0);
    
    if (GetCVar("drpg_debug"))
        Log("\cdDEBUG: \c-Checking Compatibility...\n");
    
    // Extras
    Success = SpawnForced("DRPGExtrasIsLoaded", 0, 0, 0, TID, 0);
    if (Success)
    {
        if (GetCVar("drpg_debug"))
            Log("\cdDEBUG: \caExtras\c- detected\n");
        SetCVar("drpg_ext_extras", 1);
        Thing_Remove(TID);
        return;
    };
    
    // DoomRL
    Success = SpawnForced("RLKateMatterstormHarnessArmorToken", 0, 0, 0, TID, 0);
    if (Success)
    {
        if (GetCVar("drpg_debug"))
            Log("\cdDEBUG: \cdDoomRL \c-detected\n");
        SetCVar("drpg_ext_doomrl", 1);
        Thing_Remove(TID);
        return;
    };
    
    // Brutal Doom
    Success = SpawnForced("RevenantRocketFire1823", 0, 0, 0, TID, 0);
    if (Success)
    {
        if (GetCVar("drpg_debug"))
            Log("\cdDEBUG: \cgBrutal Doom \c-detected\n");
        SetCVar("drpg_ext_brutal", 1);
        Thing_Remove(TID);
        return;
    };
};

function void AssignTIDs()
{
    Player.TID = PLAYER_TID + PlayerNumber();
    Thing_ChangeTID(0, Player.TID);
    
    if (GetCVar("drpg_debug"))
        Log("\cdDEBUG: Player TID: %d\n", Player.TID);
};
