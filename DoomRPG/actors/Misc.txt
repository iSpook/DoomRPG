// Icons
Actor DRPGMenuIcon
{
    +NOTIMEFREEZE
    +NOINTERACTION
    
    RenderStyle Add
    
    Scale 0.75
    
    States
    {
    Spawn:
        MNIC A 2 Bright
        Stop
    }
}

Actor DRPGLevelUpArrow
{
    +NOTIMEFREEZE
    +NOINTERACTION
    
    RenderStyle Add
    
    States
    {
    Spawn:
        ARRW A 0 Bright NoDelay A_ChangeVelocity(FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), 1, CVF_REPLACE)
    Fade:
        ARRW A 1 Bright A_FadeOut(0.01, true)
        Loop
    }
}

Actor DRPGRankUpArrow
{
    +NOTIMEFREEZE
    +NOINTERACTION
    
    RenderStyle Add
    
    States
    {
    Spawn:
        ARRW B 0 Bright NoDelay A_ChangeVelocity(FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), 1, CVF_REPLACE)
    Fade:
        ARRW B 1 Bright A_FadeOut(0.01, true)
        Loop
    }
}

Actor DRPGPOIArrow
{
    Radius 1
    Height 1
    
    RenderStyle Translucent
    Alpha 0.5
    
    +NOGRAVITY
    +NOBLOCKMAP
    +NOLIFTDROP
    +NOTARGET
    +DONTMORPH
    +DONTSQUASH
    +NOTELEOTHER
    +FLOATBOB
    +NONSHOOTABLE
    
    States
    {
    Spawn:
        ARRW C 4 Bright
        Loop
    }
}

Actor DRPGAlertIcon
{
    +NOINTERACTION
    
    Scale 0.5
    RenderStyle Add
    
    States
    {
    Spawn:
        ALRT A 10 Bright
    Fade:
        ALRT A 1 Bright A_FadeOut(0.05, true)
        Loop
    }
}

// Forcefield Generator
ACTOR DRPGForcefieldGenerator
{
    Radius 8
    Height 8
    Speed 0
    
    RenderStyle Translucent
    Alpha 1
    
    +NOCLIP
    +NOINTERACTION
    
    States
    {
    Spawn:
        TNT1 A 0
    NormalLoop:
        FFGN ABCDCB 1 Bright
        Goto NormalLoop
    6PowerLoop:
        FFGN ABCDCB 1 Bright
        Goto 6PowerLoop
    5PowerLoop:
        FFGN E 1 Bright
        Goto 5PowerLoop
    4PowerLoop:
        FFGN F 1 Bright
        Goto 4PowerLoop
    3PowerLoop:
        FFGN G 1 Bright
        Goto 3PowerLoop
    2PowerLoop:
        FFGN H 1 Bright
        Goto 2PowerLoop
    1PowerLoop:
        FFGN I 1 Bright
        Goto 1PowerLoop
    0PowerLoop:
        FFGN J 1 Bright
        Goto 0PowerLoop
    Death:
        FFGN K 35 Bright
        FFGN K 1 Bright A_FadeTo (0, 0.02, 1)
        Goto Death+1
    }
}

// Bouncy Balls!
Actor DRPGBouncyBall 19998
{
    var int user_type;
    
    Radius 8
    Height 32
    Mass 1
    Gravity 0.5
    Speed 0
    
    BounceType "Doom"
    BounceFActor 1.0
    
    +SOLID
    +MISSILE
    +PUSHABLE
    +SHOOTABLE
    +NOBLOOD
    +BOUNCEONACTORS
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ThrustThing(Angle, Random(4, 8), false, 0)
        TNT1 A 0 A_Jump(256, "Ball1", "Ball2", "Ball3", "Ball4", "Ball5", "Ball6")
        Stop
    Ball1:
        TNT1 A 0 A_SetUserVar("user_type", 1)
        SOUL ABCDCB 6 Bright
        Loop
    Ball2:
        TNT1 A 0 A_SetUserVar("user_type", 2)
        MEGA ABCD 6 Bright
        Loop
    Ball3:
        TNT1 A 0 A_SetUserVar("user_type", 3)
        PINV ABCD 6 Bright
        Loop
    Ball4:
        TNT1 A 0 A_SetUserVar("user_type", 4)
        PINS ABCD 6 Bright
        Loop
    Ball5:
        TNT1 A 0 A_SetUserVar("user_type", 5)
        TIME ABCDCB 4 Bright
        Loop
    Ball6:
        TNT1 A 0 A_SetUserVar("user_type", 6)
        REGN ABCDCB 4 Bright
        Loop
    Death:
        TNT1 A 0 ThrustThing(Angle, Random(4, 8), false, 0)
        TNT1 A 0 A_JumpIf(user_type == 1, "Ball1")
        TNT1 A 0 A_JumpIf(user_type == 2, "Ball2")
        TNT1 A 0 A_JumpIf(user_type == 3, "Ball3")
        TNT1 A 0 A_JumpIf(user_type == 4, "Ball4")
        TNT1 A 0 A_JumpIf(user_type == 5, "Ball5")
        TNT1 A 0 A_JumpIf(user_type == 6, "Ball6")
        Stop
    }
}

// Player Teleport Flag Setter
actor DRPGPlayerTeleportGhost : CustomInventory
{
    +INVENTORY.AUTOACTIVATE
    
    States
    {
    Use:
        TNT1 A 0 A_ChangeFlag("SOLID", false)
        Stop
    }
}

actor DRPGPlayerTeleportNormal : CustomInventory
{
    +INVENTORY.AUTOACTIVATE
    
    States
    {
    Use:
        TNT1 A 0 A_ChangeFlag("SOLID", true)
        Stop
    }
}

// Outpost Alarm

Actor DRPGOutpostAlarm : SwitchableDecoration 11012
{
    +SpawnCeiling
    +NoGravity
    
    States
    {
    Spawn:
        OALM C 0 NoDelay Thing_ChangeTID (0, 1600)
    Inactive:
        OALM C -1
        Stop
    Active:
        OALM A 0 A_Jump (256, 1, 2, 3)
        OALM AAA 1 Light ("OUTPOSTALARM")
    AlarmLoop:
        OALM A 4 Light ("OUTPOSTALARM") A_PlaySound ("misc/alarm", CHAN_VOICE, 1.0, 1, ATTN_NORM)
        OALM BCD 4 Light ("OUTPOSTALARM")
        Loop
    }
}

// Fancy Outpost Particle Effects

Actor DRPGOutpostLevelTransportParticleSpawner 11011
{
    +NoBlockmap
    +NoSector
    +NoGravity
    RenderStyle None
    
    var int user_angle;
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_SpawnItemEx ("DRPGOutpostLevelTransportParticle", 32,  0, 0, 0, 0, 0,  user_angle)
        TNT1 A 0         A_SpawnItemEx ("DRPGOutpostLevelTransportParticle", 64,  0, 0, 0, 0, 0, -user_angle)
        TNT1 A 0         A_SpawnItemEx ("DRPGOutpostLevelTransportParticle", 96,  0, 0, 0, 0, 0,  user_angle)
        TNT1 A 0         A_SpawnItemEx ("DRPGOutpostLevelTransportParticle", 128, 0, 0, 0, 0, 0, -user_angle)
        TNT1 A 3 A_SetUserVar ("user_angle", user_angle + 6)
        Loop
    }
}

Actor DRPGOutpostLevelTransportParticle
{
    +NoInteraction
    
    RenderStyle Add
    Alpha 1.0
    Scale 0.05
    
    States
    {
    Spawn:
        PTCL A 1 Bright A_SetScale (ScaleX - (0.05 / 70))
        TNT1 A 0 A_ChangeVelocity (0, 0, 0.1)
        TNT1 A 0 A_JumpIf (ScaleX <= 0, "Death")
        Loop
    Death:
        TNT1 A 0
        Stop
    }
}

// Automap Markers

Actor OutpostMarker : MapMarker
{
    Scale 0.25
    RenderStyle Add
}

Actor OutpostMarker_ArmorPad : OutpostMarker 12000
{
    States
    {
    Spawn:
        OMRK A -1
        Stop
    }
}

Actor OutpostMarker_AugBatteryPad : OutpostMarker 12001
{
    States
    {
    Spawn:
        OMRK B -1
        Stop
    }
}

Actor OutpostMarker_EPPad : OutpostMarker 12002
{
    States
    {
    Spawn:
        OMRK C -1
        Stop
    }
}

Actor OutpostMarker_HealthPad : OutpostMarker 12003
{
    States
    {
    Spawn:
        OMRK D -1
        Stop
    }
}

Actor OutpostMarker_ItemLockers : OutpostMarker 12004
{
    States
    {
    Spawn:
        OMRK E -1
        Stop
    }
}

Actor OutpostMarker_MissionBBS : OutpostMarker 12005
{
    States
    {
    Spawn:
        OMRK F -1
        Stop
    }
}

Actor OutpostMarker_Shop : OutpostMarker 12006
{
    States
    {
    Spawn:
        OMRK G -1
        Stop
    }
}

Actor OutpostMarker_LevelTransporter : OutpostMarker 12007
{
    States
    {
    Spawn:
        OMRK H -1
        Stop
    }
}

Actor OutpostMarker_SkillComputer : OutpostMarker 12008
{
    States
    {
    Spawn:
        OMRK I -1
        Stop
    }
}

Actor OutpostMarker_TokenConverter : OutpostMarker 12009
{
    States
    {
    Spawn:
        OMRK J -1
        Stop
    }
}

// Misc hacky crap

// Time Freeze for Menus
Actor DRPGMenuFreezer : PowerTimeFreezer
{
    Powerup.Duration 2
}

Actor DRPGTimeFreezerSound
{
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_PlaySound("slomo/in", CHAN_AUTO | CHAN_NOPAUSE, 1.0, false, ATTN_NONE)
        TNT1 A 0 
        TNT1 A 0 A_PlaySound("slomo/loop", CHAN_AUTO | CHAN_NOPAUSE, 1.0, true, ATTN_NONE)
        TNT1 A 7
        TNT1 A 2
        TNT1 A 0 A_PlaySound("slomo/out", CHAN_AUTO | CHAN_NOPAUSE, 1.0, false, ATTN_NONE)
        TNT1 A 35
        Stop
    }
}

Actor DRPGTimeFreezerQuickSound
{
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_PlaySound("slomo/in", CHAN_AUTO | CHAN_NOPAUSE, 1.0, false, ATTN_NONE)
        TNT1 A 0
        TNT1 A 0 A_PlaySound("slomo/loop", CHAN_AUTO | CHAN_NOPAUSE, 1.0, true, ATTN_NONE)
        TNT1 A 7
        TNT1 A 180
        TNT1 A 0 A_PlaySound("slomo/out", CHAN_AUTO | CHAN_NOPAUSE, 1.0, false, ATTN_NONE)
        TNT1 A 35
        Stop
    }
}

Actor DRPGSpeed : PowerupGiver
{
    -INVBAR
    +INVENTORY.AUTOACTIVATE
    
    Powerup.Type DoubleFiringSpeed
    Powerup.Duration 2
}

Actor DRPGShotSpawn
{
    Mass 5
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_SpawnItemEx("DRPGDumbShotgun", Random(0, 80), 0, 0, 0, Random(-30, 30))
        TNT1 A 1 A_Jump(254, "Spawn")
        Stop
    }
}

Actor DRPGDumbShotgun
{
    Mass 10
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_PlaySound("weapons/shotgr")
        SHOT A 50 A_SetTics(Random(40, 70))
        TNT1 A 0 A_PlaySound("weapons/shotgf")
        TNT1 A 0 A_SetAngle(Random(0, 256))
        TNT1 A 0 A_ChangeFlag("VISIBILITYPULSE", true)
        TNT1 A 0 A_SetTranslucent(1.0, 1)
        SHOT A 10 Bright A_CustomBulletAttack(30, 45, Random(5, 25), 1, "BulletPuff", 9999.0, CBAF_AIMFACING)
        SHOT A 105
        TNT1 A 0 A_SetScale(4.0, 4.0)
        TNT1 A 0 A_SetTranslucent(1.0, 0)
        TNT1 A 0 A_PlaySound("weapons/rocklx")
        MISL B 3 Bright A_Explode(3, 64, XF_NOTMISSILE, 256, 1)
        MISL C 3 Bright 
        MISL D 3 Bright 
        Stop
    }
}

Actor DRPGDumbPistol
{
    States
    {
    Spawn:
        PIST A 0 NoDelay A_ChangeVelocity(FRandom(-4.0, 4.0), FRandom(-4.0, 4.0), FRandom(0.0, 32.0))
        PIST A -1
        Stop
    }
}
