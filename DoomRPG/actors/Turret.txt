// Railbot base code by Captain Toenail

ACTOR DRPGOutpostTurret 19997
{
    Health 150
    Radius 16
    Height 16
    Speed 0
    Painchance 256
    Minmissilechance 1
    Mass 0x7fffffff
    Damagefactor "Melee", 0.2
    Damagefactor "Bullet", 0.2
    Damagefactor "Fire", 0.75
    Damagefactor "Plasma", 1.2
    obituary "%o forgot to check the ceiling."
    painsound ""
    deathsound "drpgturret/death"
    Monster
    +AMBUSH
    +QUICKTORETALIATE
    +SPAWNCEILING
    +CEILINGHUGGER
    +NOGRAVITY
    +NOBLOOD
    +NOSPLASHALERT
    +DONTFALL
    -COUNTKILL
    +NEVERRESPAWN
    +NOINFIGHTING
    +NOTELEOTHER
    +DONTMORPH
    +DONTBLAST
    +SEEINVISIBLE
    Species "Marine"
    Tag "Defense Turret"
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0, MF_NOXP | MF_NOSTATS | MF_NOAURA | MF_NODROPS)
        TNT1 A 0
        Goto Idle
    Idle:
        TNT1 A 0 A_CheckFlag("NEVERTARGET", "PoweredDown", AAPTR_DEFAULT)
        TNT1 A 0 A_CheckFlag("NOPAIN", "FuriousIdle", AAPTR_DEFAULT)
        TNT1 A 0 A_PlaySound ("drpgturret/hum", 6, 1, 1, 1.3)
        OTUR A 1
        Loop
    PoweredDown:
        TNT1 A 0 A_ChangeFlag ("NEVERTARGET", 1)
        TNT1 A 0 A_ChangeFlag ("NOPAIN", 0)
        TNT1 A 0 A_StopSound (6)
        OTUR A 1
        Loop
    FuriousIdle:
        TNT1 A 0 A_PlaySound ("drpgturret/hum", 6, 1, 1, 1.3)
        OTUR AAAAAAAAAAAAAAAAAAAA 1 A_Look
        TNT1 A 0 A_Jump (230, "FuriousIdle")
        TNT1 A 0 A_PlaySound ("drpgturret/beep", 7)
        Loop
    See:
        TNT1 A 0 A_CheckFlag("NEVERTARGET", "PoweredDown", AAPTR_DEFAULT)
        TNT1 A 0 A_PlaySound ("drpgturret/hum", 6, 1, 1, 1.3)
        OTUR AAAAAAAAAAAAAAAAAAAA 1 A_Chase ("", "Missile")
        TNT1 A 0 A_Jump (230, "See")
        TNT1 A 0 A_PlaySound ("drpgturret/beep", 7)
        Loop
    Missile:
        TNT1 A 0 A_PlaySound ("drpgturret/targeting", 7)
        OTUR AAAAAAAAAAAAAAA 2 A_FaceTarget (15)
    MissileLoop:
        TNT1 A 0 A_FaceTarget
        TNT1 A 0 A_PlaySound("drpgturret/fire", CHAN_WEAPON, 1, 0, 0.6)
        OTUR B 2 Bright A_CustomBulletAttack (16, 0, 2, random(1,5) * 3, "DRPGMarineBulletPuff", 0, CBAF_NORANDOM)
        OTUR C 2 Bright
        OTUR A 3 A_FaceTarget
        TNT1 A 0 A_MonsterRefire(0, "MissileEnd")
        goto MissileLoop
    MissileEnd:
        TNT1 A 0 A_PlaySound ("drpgturret/cooldown", 5)
        OTUR AAAAA 3 A_FaceTarget
        goto See
    Pain:
        TNT1 A 0 A_CheckFlag("NEVERTARGET", "Stahp", AAPTR_DEFAULT)
        TNT1 A 0 A_CheckFlag("AMBUSH", "GotShot", AAPTR_DEFAULT)
        Goto PainContinue
    PainContinue:
        OTUR A 2 A_Pain
        goto See
    GotShot:
        TNT1 A 0 ACS_NamedExecuteAlways("PissOffMarines", 0)
        Goto Enraged
    Stahp:
        TNT1 A 0 ACS_NamedExecuteAlways("PissOffMarines", 0)
        Goto Enraged
    Enraged:
        TNT1 A 0 A_CheckFlag("NEVERTARGET", "PoweredDown", AAPTR_DEFAULT)
        TNT1 A 0 A_ChangeFlag ("AMBUSH", 0)
        TNT1 A 0 A_ChangeFlag ("NOPAIN", 1)
        TNT1 A 0 A_PlaySound ("drpgturret/activate", 5)
        OTUR A 80
        Goto See
    Death:
        TNT1 A 0 A_CheckFlag("FRIENDLY", "DeathHatred", AAPTR_DEFAULT)
        TNT1 A 0 A_StopSound (6)
        TNT1 A 0 A_Scream
        TNT1 A 0 A_SpawnItemEx ("DRPGOutpostTurretExplosion", 0, 0, -4, 0, 0, 0, 0, 128)
    DeathLoop:
        TNT1 A 0 A_PlaySound ("drpgturret/spark", 5, 1, 0, 1.5)
        TNT1 A 0 A_SpawnItemEx ("DRPGOutpostTurretSpark", frandom(-12, 12), frandom(-12, 12), 2, 0, 0, 0, 0, 128)
        TNT1 A 0 A_Jump (256, 1,2,3,4,5,6,7)
        OTUR DDDDDD 10
        TNT1 A 0
        Goto DeathLoop
    DeathHatred:
        TNT1 A 0 ACS_NamedExecuteAlways("PissOffMarines", 0)
        Goto Death+1
    }
}

Actor DRPGOutpostTurretSpark
{
    radius 2
    height 2
    speed 0
    damage 0
    scale 0.18
    RenderStyle Add
    Alpha 0.75
    Translation "240:247=196:196", "192:207=192:195"
    +MISSILE
    +LOWGRAVITY
    +NOBLOCKMAP
    +NOTELEPORT
    +NOCLIP
    +DONTSPLASH
    -BLOODSPLATTER
    states
    {
    Spawn:
        TNT1 A 0 NoDelay A_Jump(128, 2)
        PLSS A 2 bright A_FadeOut(0.1)
        Goto Spawn+1
        PLSS B 2 bright A_FadeOut(0.1)
        Goto Spawn+2
    }
}

actor DRPGOutpostTurretExplosion
{
    Radius 2
    Height 2
    Speed 0
    Damage 0
    Scale 0.75
    renderstyle Add
    Projectile
    +NOCLIP
    States
    {
    Spawn:
    Death:
        TNT1 A 0
        TNT1 A 0 A_PlaySound ("drpgmarines/smallexp")
        MISL B 8 bright
        MISL C 6 bright
        MISL D 4 bright
        stop
    }
}

// PORTABLE TURRET
// --------------------------------------------------

ACTOR DRPGPortableTurret : DRPGOutpostTurret
{
    var int user_tid;
    
    Health 100
    
    Species "Player"
    
    -SOLID
    -SPAWNCEILING
    -CEILINGHUGGER
    +NOCLIP
    +NOBLOCKMAP
    +FLOATBOB
    +FRIENDLY
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay ACS_NamedExecuteAlways("MonsterInit", 0, MF_NOXP | MF_NOAURA | MF_NODROPS)
        TNT1 A 0 A_SetUservar("user_tid", CallACS("GetUniqueTID"))
        TNT1 A 0 Thing_ChangeTID(0, user_tid)
        TNT1 A 0 ACS_NamedExecuteAlways("TurretInit", 0, user_tid)
        Goto Super::See
    GotShot:
        Goto See
    DeathHatred:
        TNT1 A 0
        Stop
    Despawn:
        TNT1 A 0 A_SpawnItemEx("TeleportFog")
        Stop
    }
}

ACTOR DRPGPortableTurretPickup : CustomInventory
{
    Inventory.MaxAmount 0
    Inventory.PickupMessage "You got a Portable UAC Turret."
    Inventory.PickupSound "drpgturret/activate"
    
    States
    {
    Spawn:
        OTUR A 6 Bright
        OTUR A 6
        Loop
    Pickup:
        TNT1 A 0 A_SpawnItemEx("DRPGPortableTurret", 0, 0, 32, 0, 0, 0, 0, SXF_SETMASTER | SXF_TRANSFERTRANSLATION)
        Stop
    }
}
