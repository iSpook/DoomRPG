// Explosive Barrel
actor DRPGExplosiveBarrel : ExplosiveBarrel Replaces ExplosiveBarrel
{
    DamageType "Slime"
    
    States
    {
    Death:
        BEXP A 5 BRIGHT
        BEXP B 5 BRIGHT A_Scream
        BEXP C 5 BRIGHT
        BEXP D 5 Bright A_SpawnItemEx("DRPGExplosiveBarrelExplosion", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION | SXF_TRANSFERPOINTERS)
        BEXP E 10 BRIGHT
        TNT1 A 1050 BRIGHT A_BarrelDestroy
        TNT1 A 5 A_Respawn
        Wait
    }
}

ACTOR DRPGExplosiveBarrelExplosion : Rocket
{
    Radius 1
    Height 1
    
    +THRUGHOST
    -RANDOMIZE
    -DEHEXPLOSION
    -ROCKETTRAIL
    
    DamageType "Slime"
    
    Speed 0
    Damage 0
    SeeSound ""
    DeathSound ""
    
    Obituary "$OB_BARREL" // "%o went boom."
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_Explode
        TNT1 A 8
        Stop
    }
}

// Dead Marine
actor DRPGDeadMarine : DeadMarine Replaces DeadMarine
{
    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_SpawnItemEx("DRPGDeadMarineDropper", 0, 0, 0, Random(-4, 4), Random(-4, 4), 0, 0, SXF_NOCHECKPOSITION)
        TNT1 A 0 A_Jump(192 - (CallACS("GetSkillLevel") * 32), "Spawn")
        PLAY N -1
        Stop
    }
}

// Outpost Decorations
actor OutpostAdminLamp 12100
{
    +Solid
    Height 48
    Radius 12
    
    States
    {
    Spawn:
        COLU B -1 Light("OutpostLamp1")
        Stop
    }
}
